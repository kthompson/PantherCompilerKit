using panther

enum Chain[T] {
  case Empty()
  case Singleton(value: T)
  case Two(left: T, right: T)
  case Three(left: T, middle: T, right: T)
  case Append(left: Chain[T], right: Chain[T])
  case Wrap(value: List[T])

  def isEmpty(): bool = this match {
    case Chain.Empty()     => true
    case Chain.Wrap(value) => value.isEmpty
    case _                 => false
  }

  def toList(): List[T] = this match {
    case Chain.Empty()          => List.Nil
    case Chain.Singleton(value) => List.Cons(value, List.Nil)
    case Chain.Two(left, right) =>
      List.Cons(left, List.Cons(right, List.Nil))
    case Chain.Three(left, middle, right) =>
      List.Cons(left, List.Cons(middle, List.Cons(right, List.Nil)))
    case Chain.Append(left, right) =>
      ListModule.concat(left.toList(), right.toList())
    case Chain.Wrap(value) => value
  }

  def append(item: T): Chain[T] = this match {
    case Chain.Empty() => Chain.Singleton(item)
    case _             => this.concat(Chain.Singleton(item))
  }

  def prepend(item: T): Chain[T] = this match {
    case Chain.Empty() => Chain.Singleton(item)
    case _             => Chain.Singleton(item).concat(this)
  }

  def concat(other: Chain[T]): Chain[T] = {
    this match {
      case Chain.Empty() => other
      case Chain.Singleton(left) =>
        other match {
          case Chain.Empty()            => Chain.Singleton(left)
          case Chain.Singleton(right)   => Chain.Two(left, right)
          case Chain.Two(middle, right) => Chain.Three(left, middle, right)
          case _                        => Chain.Append(this, other)
        }
      case Chain.Two(left, middle) =>
        other match {
          case Chain.Empty()          => this
          case Chain.Singleton(right) => Chain.Three(left, middle, right)
          case _                      => Chain.Append(this, other)
        }
      case _ => Chain.Append(this, other)
    }
  }

  def uncons(): Option[Tuple2[T, Chain[T]]] = this match {
    case Chain.Empty()          => Option.None
    case Chain.Singleton(value) => Option.Some(Tuple2(value, Chain.Empty()))
    case Chain.Two(left, right) =>
      Option.Some(Tuple2(left, Chain.Singleton(right)))
    case Chain.Three(left, middle, right) =>
      Option.Some(Tuple2(left, Chain.Two(middle, right)))
    case Chain.Append(left, right) =>
      left.uncons() match {
        case Option.Some(Tuple2(head, tail)) =>
          Option.Some(Tuple2(head, tail.concat(right)))
        case Option.None => right.uncons()
      }
    case Chain.Wrap(value) =>
      value match {
        case List.Nil => Option.None
        case List.Cons(head, tail) =>
          Option.Some(Tuple2(head, Chain.Wrap(tail)))
      }
  }
}

object ChainModule {
  def fromList[A](list: List[A]): Chain[A] = list.length match {
    case 0 => Chain.Empty()
    case 1 => Chain.Singleton(list.headUnsafe())
    case _ => Chain.Wrap(list)
  }

  def flatten[A](chain: Chain[Chain[A]]): Chain[A] = {
    chain.uncons() match {
      case Option.Some(Tuple2(head, tail)) =>
        head.concat(flatten(tail))
      case Option.None => Chain.Empty()
    }
  }

  def of[A](item: A): Chain[A] =
    Chain.Singleton(item)
}

class ChainEnumerator[A](chain: Chain[A]) {
  var moveFirst = true
  var _current: Chain[A] = chain

  def moveNext(): bool = {
    _current.uncons() match {
      case Option.Some(Tuple2(_, tail)) =>
        if (moveFirst) {
          moveFirst = false
        } else {
          _current = tail
        }
        !_current.isEmpty()
      case Option.None => false
    }
  }

  def current(): A = _current.uncons() match {
    case Option.Some(Tuple2(head, _)) => head
    case Option.None                  => panic("No current element")
  }

  def reset(): unit = {
    _current = chain
    moveFirst = true
  }
}
