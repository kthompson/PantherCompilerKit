using panther

class IndentedStringBuilder(useConsole: bool) {
  val indentValue: string = "  "

  var currentIndent: string = ""
  var sb: StringBuilder = StringBuilder()
  var needsIndent: bool = false

  def appendChar(value: char): unit = {
    if (needsIndent) {
      if (useConsole) {
        print(currentIndent)
      } else {
        sb.append(currentIndent)
      }
      needsIndent = false
    }

    if (value == '\n') {
      needsIndent = true
    }

    if (useConsole) {
      print(value)
    } else {
      sb.appendChar(value)
    }
  }

  def appendString(str: string, index: int, count: int): unit = {
    if (str.length > 0 && index >= 0 && index < str.length && count > 0) {
      appendChar(str(index))
      appendString(str, index + 1, count - 1)
    }
  }

  def append(value: string): unit = appendString(value, 0, value.length)

  def appendLine(value: string): unit =
    append(value + "\n")

  def indent(): unit = {
    currentIndent = currentIndent + indentValue
  }

  def unindent(): unit = {
    if (currentIndent.length >= indentValue.length) {
      currentIndent =
        currentIndent.substring(0, currentIndent.length - indentValue.length)
    }
  }

  def toString(): string = sb.toString()
}
