using panther

class SymbolPrinter(
    binder: Binder,
    ast: AstPrinter
) {

  def printSymbols(symbols: List[Symbol], indent: string): unit = {
    symbols match {
      case List.Nil => ()
      case List.Cons(head, tail) =>
        _printSymbol(head, indent, tail.isEmpty)
        if (!tail.isEmpty) {
          printSymbols(tail, indent)
        }
    }
  }

  def printSymbol(symbol: Symbol): unit = _printSymbol(symbol, "", true)

  def _printSymbol(symbol: Symbol, indent: string, last: bool): unit = {
    val marker = if (last) "└──" else "├──"

    ast.writeColor(ColorPalette.Comment)
    ast.append(indent)
    ast.append(marker)
    ast.append(ANSI.Clear)

    val kind = symbol.kind
    ast.printSymbolKind(kind)
    ast.append(" ")

    printSimpleSymbol(symbol)
    printSymbolBodyType(symbol)
    ast.appendLine("")

    val members = symbol.members()
    if (!members.isEmpty) {
      val end = if (last) "    " else "│   "
      val childIndent = indent + end
      printSymbols(members, childIndent)
    }
  }

  private def printSymbolBodyType(symbol: Symbol): unit = {
    if (
      symbol.kind == SymbolKind.Constructor || symbol.kind == SymbolKind.Method
    ) {
      ast.writeColor(ColorPalette.Punctuation)
      ast.append(" = ")
      if (symbol.extern) {
        ast.writeColor(ColorPalette.Keyword)
        ast.append("[extern]")
      } else if (binder.functionBodies.contains(symbol)) {
        ast.writeColor(ColorPalette.Keyword)
        ast.append("[body]")
      } else {
        ast.writeColor(ColorPalette.Error)
        ast.append("[no body]")
      }
      ast.append(ANSI.Clear)
    }
  }

  def printSimpleSymbol(symbol: Symbol): unit = {
    if (
      symbol.kind == SymbolKind.Class || symbol.kind == SymbolKind.Object || symbol.kind == SymbolKind.Alias
    ) {
      val typ = binder.tryGetSymbolType(symbol)
      typ match {
        case Option.Some(value) =>
          ast.printType(value)
        case Option.None =>
          ast.append(symbol.qualifiedName())
      }
    } else {
      ast.append(symbol.name)
    }
    ast.writeColor(ColorPalette.Punctuation)

    if (
      symbol.kind != SymbolKind.Namespace &&
      symbol.kind != SymbolKind.Block &&
      symbol.kind != SymbolKind.Alias &&
      symbol.kind != SymbolKind.Object
    ) {
      val typ = binder.tryGetSymbolType(symbol)
      typ match {
        case Option.Some(value) =>
          symbol.kind match {
            case SymbolKind.Class            =>
            case SymbolKind.TypeParameter(_) =>
            case _ =>
              ast.append(": ")
              ast.printType(value)
          }
        case Option.None =>
          ast.append(": ")
          ast.writeColor(ColorPalette.Error)
          ast.append("[missing type]")
          ast.append(ANSI.Clear)
      }
    }
  }
}
